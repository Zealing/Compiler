/*
 *  cool.cup
 *              Parser definition for the COOL language.
 *
 */

import java_cup.runtime.*;

/* Stuff enclosed in {: :} is copied verbatim to the class containing
all parser actions.  All the extra variables/functions you want to use
in the semantic actions should go here.  Don't remove or modify anything
that was there initially.  */

action code {: 

    int curr_lineno() {
	return ((CoolTokenLexer)parser.getScanner()).curr_lineno();
    }

    AbstractSymbol curr_filename() {
	return ((CoolTokenLexer)parser.getScanner()).curr_filename();
    }
:} 

/************************************************************************/
/*                DONT CHANGE ANYTHING IN THIS SECTION                  */

parser code {:
    int omerrs = 0;

    public void syntax_error(Symbol cur_token) {
        int lineno = action_obj.curr_lineno();
	String filename = action_obj.curr_filename().getString();
        System.err.print("\"" + filename + "\", line " + lineno + 
		         ": parse error at or near ");
        Utilities.printToken(cur_token);
	omerrs++;
	if (omerrs>50) {
	   System.err.println("More than 50 errors");
	   System.exit(1);
	}
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
    }
:}

/* Declare the terminals; a few have types for associated lexemes.  The
token ERROR is never used in the parser; thus, it is a parse error when
the lexer returns it.  */

terminal CLASS, ELSE, FI, IF, IN, INHERITS, LET, LET_STMT, LOOP, POOL, THEN, WHILE;
terminal CASE, ESAC, OF, DARROW, NEW, ISVOID;
terminal ASSIGN, NOT, LE, ERROR;
terminal PLUS, DIV, MINUS, MULT, EQ, LT, DOT, NEG, COMMA, SEMI, COLON;
terminal LPAREN, RPAREN, AT, LBRACE, RBRACE;
terminal AbstractSymbol STR_CONST, INT_CONST;
terminal Boolean BOOL_CONST;
terminal AbstractSymbol TYPEID, OBJECTID;

/*  DON'T CHANGE ANYTHING ABOVE THIS LINE, OR YOUR PARSER WONT WORK       */
/**************************************************************************/

   /* Complete the nonterminal list below, giving a type for the semantic
      value of each non terminal. (See the CUP documentation for details. */

nonterminal programc program;
nonterminal Classes class_list;
nonterminal class_c class;
nonterminal Features feature_list;
nonterminal Feature feature;
nonterminal Expressions expr_list;
nonterminal Expression expr;
nonterminal Formals formal_list;
nonterminal Formal	formal;
nonterminal Expressions expr_block;
nonterminal Cases case_list;
nonterminal Case case;
nonterminal Expression let_tail;


/* Precedence declarations go here. */
precedence right ASSIGN;
precedence left NOT;
precedence nonassoc LE, LT, EQ;
precedence left PLUS, MINUS;
precedence left MULT, DIV;
precedence left ISVOID;
precedence left NEG;
precedence left AT;
precedence left DOT;

program	
	::= class_list:cl
	    {: RESULT = new programc(curr_lineno(), cl); :}
	;

class_list
	/* single class */
	::= class:c
	    {: RESULT = (new Classes(curr_lineno())).appendElement(c); :}
	/* several classes */
	| class_list:cl class:c
	    {: RESULT = cl.appendElement(c); :}
	| error
		{: RESULT = new Classes(curr_lineno()); :}
	;

/* If no parent is specified, the class inherits from the Object class */
class
	::= CLASS TYPEID:n LBRACE feature_list:f RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, 
		                   AbstractTable.idtable.addString("Object"), 
				   f, curr_filename()); :}
	| CLASS TYPEID:n INHERITS TYPEID:p LBRACE feature_list:f RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, p, f, curr_filename()); :}
	;

/* Feature list may be empty, but no empty features in list. */
feature_list
	/* none */
	::= /* empty */
		{: RESULT = new Features(curr_lineno()); :}
	| feature:f SEMI
		{: RESULT = (new Features(curr_lineno())).appendElement(f); :}
	| feature_list:fl feature:f SEMI
		{: RESULT = fl.appendElement(f); :}
	| error SEMI
		{: RESULT = new Features(curr_lineno()); :}
	;
	
feature
	::= OBJECTID:o LPAREN formal_list:fl RPAREN COLON TYPEID:t LBRACE expr:e RBRACE
		{: RESULT = new method(curr_lineno(), o, fl, t, e); :}
	| OBJECTID:o COLON TYPEID:t
		{: RESULT = new attr(curr_lineno(), o, t, (new no_expr(curr_lineno()))); :}
	| OBJECTID:o COLON TYPEID:t ASSIGN expr:e
		{: RESULT = new attr(curr_lineno(), o, t, e); :}
	;
	
formal_list
	::= /* empty */
		{: RESULT = new Formals(curr_lineno()); :}
	| formal:fo
		{: RESULT = (new Formals(curr_lineno())).appendElement(fo); :}
	| formal_list:fol COMMA formal:fo
		{: RESULT = fol.appendElement(fo); :}
	| error
		{: RESULT = new Formals(curr_lineno()); :}
	;
	
formal
	::= OBJECTID:o COLON TYPEID:t
		{: RESULT = new formalc(curr_lineno(), o, t); :}
	;

expr_list
	::= /* empty */
		{: RESULT = new Expressions(curr_lineno()); :}
	| expr:e
		{: RESULT = (new Expressions(curr_lineno())).appendElement(e); :}
	| expr_list:el COMMA expr:e
		{: RESULT = el.appendElement(e); :}
	| error
		{: RESULT = new Expressions(curr_lineno()); :}
	;

expr
	::= OBJECTID:o ASSIGN expr:e
		{: RESULT = new assign(curr_lineno(), o, e); :}
	| expr:e DOT OBJECTID:o LPAREN expr_list:el RPAREN
		{: RESULT = new dispatch(curr_lineno(), e, o, el); :}
	| expr:e AT TYPEID:t DOT OBJECTID:o LPAREN expr_list:el RPAREN
		{: RESULT = new static_dispatch(curr_lineno(), 
										e, t, o, el); :}
	| OBJECTID:o LPAREN expr_list:el RPAREN
		{: RESULT = new dispatch(curr_lineno(), 
							(new object(curr_lineno(), 
							AbstractTable.idtable.addString("self"))), 
							o, el); :}
	| IF expr:e1 THEN expr:e2 ELSE expr:e3 FI
		{: RESULT = new cond(curr_lineno(), e1, e2, e3); :}
	| WHILE expr:e1 LOOP expr:e2 POOL
		{: RESULT = new loop(curr_lineno(), e1, e2); :}
	| WHILE expr LOOP expr LOOP error
		{: RESULT = new no_expr(curr_lineno()); :}
	| LBRACE expr_block:eb RBRACE
		{: RESULT = new block(curr_lineno(), eb); :}
	| LET OBJECTID:o COLON TYPEID:t IN expr:e
		{: RESULT = new let(curr_lineno(), o, t, (new no_expr(curr_lineno())), e); :}
	| LET OBJECTID:o COLON TYPEID:t ASSIGN expr:e1 IN expr:e2
		{: RESULT = new let(curr_lineno(), o, t, e1, e2); :}
	| LET OBJECTID:o COLON TYPEID:t let_tail:lt
		{: RESULT = new let(curr_lineno(), o, t, 
							(new no_expr(curr_lineno())),
							lt); :}
	| LET OBJECTID:o COLON TYPEID:t ASSIGN expr:e let_tail:lt
		{: RESULT = new let(curr_lineno(), o, t, e, lt); :}
	| CASE expr:e OF case_list:cl ESAC
		{: RESULT = new typcase(curr_lineno(), e, cl); :}
	| NEW TYPEID:t
		{: RESULT = new new_(curr_lineno(), t); :}
	| ISVOID expr:e
		{: RESULT = new isvoid(curr_lineno(), e); :}
	| expr:e1 PLUS expr:e2
		{: RESULT = new plus(curr_lineno(), e1, e2); :}
	| expr:e1 MINUS expr:e2
		{: RESULT = new sub(curr_lineno(), e1, e2); :}
	| expr:e1 MULT expr:e2
		{: RESULT = new mul(curr_lineno(), e1, e2); :}
	| expr:e1 DIV expr:e2
		{: RESULT = new divide(curr_lineno(), e1, e2); :}
	| NEG expr:e
		{: RESULT = new neg(curr_lineno(), e); :}
	| expr:e1 LT expr:e2
		{: RESULT = new lt(curr_lineno(), e1, e2); :}
	| expr:e1 LE expr:e2
		{: RESULT = new leq(curr_lineno(), e1, e2); :}
	| expr:e1 EQ expr:e2
		{: RESULT = new eq(curr_lineno(), e1, e2); :}
	| NOT expr:e
		{: RESULT = new comp(curr_lineno(), e); :}
	| LPAREN expr:e RPAREN
		{: RESULT = e; :}
	| OBJECTID:o
		{: RESULT = new object(curr_lineno(), o); :}
	| INT_CONST:i
		{: RESULT = new int_const(curr_lineno(), i); :}
	| STR_CONST:s
		{: RESULT = new string_const(curr_lineno(), s); :}
	| BOOL_CONST:b
		{: RESULT = new bool_const(curr_lineno(), b); :}
	| error
		{: RESULT = new no_expr(curr_lineno()); :}
	;
	
expr_block
	::= expr:e SEMI
		{: RESULT = (new Expressions(curr_lineno())).appendElement(e); :}
	| expr_block:eb expr:e SEMI
		{: RESULT = eb.appendElement(e); :}
	| error SEMI
		{: RESULT = new Expressions(curr_lineno()); :}
	;
	
case_list
	::= case:c SEMI
		{: RESULT = (new Cases(curr_lineno())).appendElement(c); :}
	| case_list:cl case:c SEMI
		{: RESULT = cl.appendElement(c); :}
	;
	
case
	::= OBJECTID:o COLON TYPEID:t DARROW expr:e
		{: RESULT = new branch(curr_lineno(), o, t, e); :}
	;

let_tail
	::= COMMA OBJECTID:o COLON TYPEID:t let_tail:tl
		{: RESULT = new let(curr_lineno(), o, t,
							(new no_expr(curr_lineno())), tl); :}
	| COMMA OBJECTID:o COLON TYPEID:t IN expr:e
		{: RESULT = new let(curr_lineno(), o, t,
							(new no_expr(curr_lineno())), e); :}
	| COMMA OBJECTID:o COLON TYPEID:t ASSIGN expr:e let_tail:tl
		{: RESULT = new let(curr_lineno(), o, t, e, tl); :}
	| COMMA OBJECTID:o COLON TYPEID:t ASSIGN expr:e1 IN expr:e2
		{: RESULT = new let(curr_lineno(), o, t, e1, e2); :}
	;
